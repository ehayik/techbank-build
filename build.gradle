buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.29")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBoot}" apply false
    id 'io.spring.dependency-management' version "${dependencyManagement}"
}

project.version = "0.0.1"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {

    repositories {
        mavenCentral()
    }
}

subprojects {

    group = 'com.github.ehayik.playground.techbank'
    version = rootProject.version

    apply plugin: 'java'
    apply plugin: 'io.spring.javaformat'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBoot}")
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += '--enable-preview'
    }

    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
    }

    tasks.withType(JavaExec) {
        jvmArgs += "--enable-preview"
    }
}
